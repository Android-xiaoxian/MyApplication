apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'me.tatarka.retrolambda'

static def apkName() {
    return "YuriTam"
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.yuri.tam"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled false
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        jackOptions {
            enabled false
        }
    }
    sourceSets.main {
        java.srcDirs = ['src/main/java', 'src/main/java-gen']
        jniLibs.srcDirs = ['libs', 'src/main/libs']
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "BASE_URL", "\"http:www.baidu.com/release/\""
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "BASE_URL", "\"http:www.baidu.com/debug/\""
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //输出apk配置
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 输出apk名称为 Payment_v1.0.0_20170816_release.apk
                if (variant.buildType.name == 'release') {
                    fileName = "${apkName()}_${defaultConfig.versionName}_${releaseTime()}_release.apk"
                } else if (variant.buildType.name == 'debug') {
                    fileName = "${apkName()}_${defaultConfig.versionName}_debug.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    greendao {
        schemaVersion 1
        daoPackage 'com.yuri.tam.databases'
        targetGenDir 'src/main/java-gen'
    }
    lintOptions {
        abortOnError false
    }
}
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.1.2'
    testCompile 'junit:junit:4.12'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.6'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.google.guava:guava:23.2-android'
    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    //json
    compile 'com.google.code.gson:gson:2.8.2'
    //okhttp log
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    //rx java
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    //图片下载
    compile 'com.squareup.picasso:picasso:2.5.2'
    //动态申请权限
    compile 'pub.devrel:easypermissions:0.4.2'
    //数据库
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'org.greenrobot:greendao-generator:3.2.2'
    //UI绑定
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //日志
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'
    compile 'org.slf4j:slf4j-api:1.7.22'
    //状态栏
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    //扫码
    compile project(':lib-qr-scan')
    //工具包
    compile project(':lib-utils')
}
